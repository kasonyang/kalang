plugins {
    id 'org.jbake.site' version '5.0.0'
}

apply plugin:'java'
apply plugin:'application'
apply from:'gradle/version.gradle'
apply from:'gradle/docs.gradle'
mainClassName = "kalang.compiler.shell.Kalangc"
startScripts.applicationName = "kalangc"

repositories{
  mavenCentral()
}

dependencies{
  compile 'org.antlr:antlr4-runtime:4.5.1'
  compile 'org.apache.commons:commons-collections4:4.1'
  testCompile 'junit:junit:4.12'
  compile 'org.ow2.asm:asm:7.3.1'
  compile 'com.google.code.findbugs:jsr305:3.0.1'
  compile project(":kalang-runtime")
  compile 'commons-io:commons-io:2.5'
  compile 'commons-cli:commons-cli:1.3.1'
  compile 'org.apache.ivy:ivy:2.5.0'
  compile 'com.fifesoft:rsyntaxtextarea:3.0.3'
  compileOnly 'org.projectlombok:lombok:1.18.8'
  annotationProcessor 'org.projectlombok:lombok:1.18.8'
}


apply from:'gradle/maven-publish.gradle'

apply plugin: 'jacoco'
jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
}



task startScriptsForShell(type: CreateStartScripts) {
  outputDir = file('build/scripts')
  mainClassName = 'kalang.compiler.shell.Kalangsh'
  applicationName = "kalangsh"
  classpath = startScripts.classpath
}

startScripts.dependsOn startScriptsForShell


task startScriptsForEditor(type: CreateStartScripts) {
  outputDir = file('build/scripts')
  mainClassName = 'kalang.compiler.shell.Kalangeditor'
  applicationName = "kalangeditor"
  classpath = startScripts.classpath
}

startScripts.dependsOn startScriptsForEditor


tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}


